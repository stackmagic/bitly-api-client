
plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.bintray'             version '1.2'
	id 'net.swisstech.eclipseenhancer' version '1.0.0'
}

description = 'Bitly API Client'
group       = 'net.swisstech'
version     = '0.8.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

// extra configuration for integration tests
configurations {
	intTestCompile
}

sourceSets {
	intTest {
		compileClasspath = sourceSets.main.output + configurations.testRuntime
		runtimeClasspath = sourceSets.main.output + configurations.testRuntime + output
	}
}

// normal repositories and dependencies
repositories {
	jcenter()
	mavenLocal()
	mavenCentral()
}

dependencies {
	// json (de)serializing
	compile 'com.google.code.gson:gson:2.3.1'

	// date and time functionality
	compile 'joda-time:joda-time:2.8.1'

	// logging
	compile 'org.slf4j:slf4j-api:1.7.12'

	// unit testing
	testCompile 'org.slf4j:jcl-over-slf4j:1.7.12'
	testCompile 'ch.qos.logback:logback-classic:1.1.3'
	testCompile 'org.testng:testng:6.8.8'

	// integration testing
	intTestCompile 'org.slf4j:jcl-over-slf4j:1.7.12'
	intTestCompile 'ch.qos.logback:logback-classic:1.1.3'
	intTestCompile 'org.testng:testng:6.8.8'
}

// testing
test.useTestNG()

// task to generate wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

// source-code jar
task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

// bintray uses the publication, also includes source jar
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar {
				classifier 'sources'
			}
		}
	}
}

def getProperty = { String name ->
	try {
		return project[name]
	}
	catch (MissingPropertyException e) {
		println "!!! property ${name} is not available"
		return ""
	}
}

apply plugin: 'com.jfrog.bintray'
bintray {
	// key and user must be in your ~/.gradle/gradle.properties
	key          = getProperty('swisstech_bintray_apikey')
	user         = getProperty('swisstech_bintray_user')
	dryRun       = false
	publish      = true
	publications = [ 'mavenJava' ]
	pkg {
		repo            = 'maven'
		name            = project.name
		desc            = project.description
		licenses        = [ 'Apache-2.0' ]
		websiteUrl      = "https://github.com/stackmagic/${project.name}"
		vcsUrl          = "https://github.com/stackmagic/${project.name}.git"
		issueTrackerUrl = "https://github.com/stackmagic/${project.name}/issues"
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project.description
		}
	}
}

task('intTest', type: Test, dependsOn: 'intTestClasses') {
	useTestNG()
	testLogging.showStandardStreams = true
	group          = 'verification'
	testSrcDirs    = sourceSets.intTest.java.srcDirs as List
	testClassesDir = sourceSets.intTest.output.classesDir
	classpath      = sourceSets.intTest.runtimeClasspath
}
